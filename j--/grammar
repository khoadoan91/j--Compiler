// A grammar for j--, suitable for parsing by recursive descent
// Copyright 2013 Bill Campbell, Swami Iyer and Bahar Akbal-Delibas

compilationUnit ::= [PACKAGE qualifiedIdentifier SEMI]
                    {IMPORT  qualifiedIdentifier SEMI}
                    {typeDeclaration} EOF 

qualifiedIdentifier ::= IDENTIFIER {DOT IDENTIFIER}

typeDeclaration ::= modifiers classDeclaration

modifiers ::= {PUBLIC | PROTECTED | PRIVATE | STATIC 
			| ABSTRACT | FINAL}

classDeclaration ::= CLASS IDENTIFIER 
                       [EXTENDS qualifiedIdentifier]
                       [IMPLEMENTS qualifiedIdentifier] 
                       classBody

classBody ::= LCURLY {modifiers memberDecl} RCURLY

memberDecl ::= IDENTIFIER            // constructor
                 formalParameters block
             | (VOID | type) IDENTIFIER  // method
                 formalParameters [THROWS qualifiedIdentifier]
                 {COMMA IDENTIFIER}
                 (block | SEMI)
             | type variableDeclarators SEMI // field

block ::= LCURLY {blockStatement} RCURLY 

blockStatement ::= localVariableDeclarationStatement
                 | statement

statement ::= block
            | IF parExpression statement [ELSE statement]
            | WHILE parExpression statement 
            | FOR LPAREN 
            	[forInit] SEMI 
            	[expression] SEMI 
            	[forUpdate]
            	RPAREN statement
            | TRY tryStatement
            | TRY block 
            	{CATCH LPAREN formalParameter RPAREN block}
            	[FINALLY block]
            | SWITCH primary LCURLY casesStatement RCURLY
            | SWITCH parExpression LCURLY {switchBlockStatementGroup} RCULY
            | DO statement UNTIL parExpression SEMI
            | RETURN [expression] SEMI
            | SEMI 
            | THROW expression SEMI
            | statementExpression SEMI

formalParameters ::= LPAREN 
                       [formalParameter {COMMA formalParameter}]
                     RPAREN

formalParameter ::= type IDENTIFIER

forInit ::= type variableDeclarators
		  	
forUpdate ::= statementExpression {COMMA statementExpression}

switchBlockStatementGroup ::= switchLabel {switchLabel} {blockStatement} 

switchLabel ::= case expression COLON // must be constant
				| default COLON

parExpression ::= LPAREN expression RPAREN

localVariableDeclarationStatement ::= type variableDeclarators SEMI

variableDeclarators ::= variableDeclarator {COMMA variableDeclarator}

variableDeclarator ::= IDENTIFIER [ASSIGN variableInitializer]
                    
variableInitializer ::= arrayInitializer | expression

arrayInitializer ::= LCURLY 
                       [variableInitializer {COMMA variableInitializer}]
                     RCURLY 

arguments ::= LPAREN [expression {COMMA expression}] RPAREN

type ::= referenceType | basicType

basicType ::= BOOLEAN | CHAR | INT | LONG | FLOAT | DOUBLE

referenceType ::= basicType LBRACK RBRACK {LBRACK RBRACK}
                | qualifiedIdentifier {LBRACK RBRACK}

statementExpression ::= expression // but must have side-effect, eg i++

expression ::= assignmentExpression

assignmentExpression ::= conditionalExpression  // must be a valid lhs - level 13
                           [(ASSIGN | PLUS_ASSIGN | MINUS_ASSIGN 
                           | STAR_ASSIGN | DIV_ASSIGN) assignmentExpression]
                           
conditionalExpression ::= conditionalOrExpression 	// level 12
				[QUESTION assignmentExpression COLON conditionalExpression]

conditionalOrExpression ::= conditionalAndExpresion // level 11
								{LOR condionalAndExpression}

conditionalAndExpression ::= bitwiseOr // level 10
                               {LAND bitwiseOr}

bitwiseOr ::= bitwiseXor 		// level 9
						{BIT_OR bitwiseXor}

bitwiseXor ::= bitwiseAnd 		// level 8
						{BIT_XOR bitwiseAnd}

bitwiseAnd ::= equalityExpression 		// level 7
						{BIT_AND equalityExpression} 

equalityExpression ::= relationalExpression  // level 6
                         {(EQUAL | NEQUAL) relationalExpression}

relationalExpression ::= shiftExpression       // level 5
                           [(GT | LE | GE | LT) shiftExpression
                           | INSTANCEOF referenceType]

shiftExpression ::= additiveExpression 			// level 4
						[(LEFT_SHIFT | RIGHT_SHIFT | URIGHT_SHIFT) additiveExpression]

additiveExpression ::= multiplicativeExpression // level 3
                         {(PLUS | MINUS) multiplicativeExpression}

multiplicativeExpression ::= unaryExpression       // level 2
                               {(STAR | DIV | REMINDER) unaryExpression}

unaryExpression ::= INC unaryExpression  // level 1
				  | DEC unaryExpression
				  | BIT_NOT unaryExpression
                  | MINUS unaryExpression
                  | simpleUnaryExpression

simpleUnaryExpression ::= LNOT unaryExpression
                        | LPAREN basicType RPAREN unaryExpression //casted
                        | LPAREN // casted               
                            referenceType
                          RPAREN simpleUnaryExpression
                        | postfixExpression 
                        
postfixExpression ::= primary {selector} {DEC | INC}

selector ::= DOT qualifiedIdentifier [arguments]
           | LBRACK expression RBRACK

primary ::= parExpression
          | THIS [arguments]
          | SUPER ( arguments 
                  | DOT IDENTIFIER [arguments] 
                  )
          | literal
          | NEW creator
          | qualifiedIdentifier [arguments]

creator ::= (basicType | qualifiedIdentifier) 
              ( arguments
              | LBRACK RBRACK {LBRACK RBRACK} [arrayInitializer]
              | newArrayDeclarator
              )

newArrayDeclarator ::= LBRACK expression RBRACK 
                         {LBRACK expression RBRACK} {LBRACK RBRACK}

literal ::= INT_LITERAL | CHAR_LITERAL | STRING_LITERAL | LONG_LITERAL | DOUBLE_LITERAL
        | FLOAT_LITERAL | TRUE         | FALSE          | NULL
